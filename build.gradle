buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath group: 'net.researchgate', name: 'gradle-release', version: '2.6.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

group = "org.grobid"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/rookies/maven" }
}
                  
dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
//    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
//    implementation 'com.google.guava:guava:22.0'

    //Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.easymock:easymock:3.4'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

    //GROBID
    implementation 'org.grobid:grobid-core:0.5.1'
    implementation 'org.grobid:grobid-trainer:0.5.1'

    //Apache commons
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'org.apache.commons:commons-lang3:3.0.1'

    // xml parsing
    implementation 'org.codehaus.woodstox:stax2-api:3.1.4'
    implementation 'com.fasterxml.woodstox:woodstox-core:5.0.3'

    //Json
    implementation 'com.fasterxml.jackson.core:jackson-core:2.7.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.7.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.7.2'

    //NLP
    implementation 'edu.emory.mathcs.nlp:nlp4j-api:1.1.3'
    implementation 'edu.emory.mathcs.nlp:nlp4j-english:1.1.3'

    //Web interface
    implementation 'com.sun.jersey:jersey-client:1.8'
    implementation 'com.sun.jersey:jersey-server:1.8'
    implementation 'com.sun.jersey:jersey-core:1.8'
    implementation 'com.sun.jersey.contribs:jersey-multipart:1.8'
    implementation 'org.jvnet:mimepull:1.6'
    implementation 'javax.ws.rs:jsr311-api:1.1.1'
    implementation 'javax.servlet:servlet-api:2.4'
    implementation 'javax.servlet.jsp:jsp-api:2.0'
}

def trainerTasks = [
        //Training models
        "train_astro"               : "org.grobid.trainer.AstroTrainer",
        "trainer"                   : "org.grobid.trainer.AstroTrainerRunner"
]

trainerTasks.each { taskName, mainClassName ->
    tasks.create(name: taskName, type: JavaExec, group: 'training') {
        main = mainClassName
        classpath = sourceSets.main.runtimeClasspath
    }
}

// return the default value if the property has not been specified in command line 
ext.getArg = { propName, defaultVal ->
    return project.hasProperty(propName) ? project.getProperty(propName) : defaultVal;
}

// Evaluation
// run like: gradle evalAstro -gH /path/to/grobid-home
task evalAstro(dependsOn: 'classes', type: JavaExec, group: 'training') {
    main = 'org.grobid.trainer.AstroTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args 1, 'astro', '-gH', getArg('gH', '../grobid-home')
    jvmArgs '-Xmx3072m'
}

// run like: gradle evalAstroSplit -gH /path/to/grobid-home
task evalAstroSplit(dependsOn: 'classes', type: JavaExec, group: 'training') {
    main = 'org.grobid.trainer.AstroTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args 2, 'astro', '-gH', getArg('gH', '../../grobid-home'), '-s', getArg('s', '0.8')
    jvmArgs '-Xmx3072m'
}


import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task mainJar(type: ShadowJar, group: 'test') {
    zip64 true
    from sourceSets.main.output
    from {
        project.configurations.compile.collect {
            it.isDirectory() ? [] : localLibs.contains(it.getName()) ? zipTree(it) : []
        }
    }
}

shadowJar {
    classifier = 'onejar'
    zip64 true
    manifest {
        attributes 'Main-Class': 'org.grobid.core.main.batch.AstroMain'
    }
}

jar {
    dependsOn mainJar
    enabled false
}

artifacts {
    archives shadowJar
    archives mainJar
}

/*tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
    options.compilerArgs << "-Xlint:unchecked"
}*/




